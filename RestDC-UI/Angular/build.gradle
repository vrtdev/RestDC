apply plugin: 'compass'

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/robfletcher/gradle-plugins' }
    }
    dependencies {
        classpath 'org.gradle.plugins:gradle-compass:1.0.7'
    }
}

repositories {
    mavenCentral()
}

def spAppSources = fileTree('src/main') {
    include '**/*.css'
    include '**/*.js'
    include '**/*.html'
    include '**/*.json'
    include 'font/**/*'
    exclude 'sass/'

}

compass {
    cssDir = file("$buildDir/stylesheets")
    sassDir = file('src/main/sass')
}

task npmInstall(type: Exec) {
    commandLine 'npm', 'install'
    ext.output = {
        return standardOutput.toString()
    }
}

task test(type: Exec, dependsOn: npmInstall) {
    commandLine './node_modules/karma/bin/karma', 'start', '--single-run'
    ext.output = {
        return standardOutput.toString()
    }
}

task build(overwrite: true, type: Zip, dependsOn: [compileSass, test]) {
    from spAppSources
    into('stylesheets') {
        from compass.cssDir
    }
}



